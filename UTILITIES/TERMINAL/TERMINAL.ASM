***********************************************
*    RS-232 COMMUNICATIONS ROUTINE FOR THE    *
*           SWTP  6800  COMPUTER              *
*                                             *
***********************************************

* DEFINITIONS:

P1CR	EQU		$8004		PORT 1 CONTROL AND STATUS REGISTERS
P1DR	EQU		$8005		PORT 1 DATA REGISTER
P1SET	EQU		$55			PORT 1 SET TO 8N1
P2CR	EQU		$8008		PORT 2 CONTROL AND STATUS REGISTERS
P2DR	EQU		$8009		PORT 2 DATA REGISTER
P2SET	EQU		$55			PORT 2 SET TO 8N1

DELAY	EQU		$E2C2 

* PROGRAM:

		ORG		$100

INIT	LDA B	#3				TO RESET ACIA'S
		LDX		#P1CR			SET INDEX TO PORT 1
		STA B	0,X				RESET PORT 1
		STA B	P2CR-P1CR,X		RESET PORT 2
		LDA A	#P1SET			TO CONFIGURE ACIA'S TO 8B+1SB
		STA A	0,X				CONFIGURE PORT 1
		STA A	P2CR-P1CR,X		CONFIGURE PORT 2
R1		LDA A	#1				PREPARE TO CHECK PORT 1 FOR DATA
		AND A	0,X				ANY DATA FROM PORT 1?
		BEQ		R2				IF NOT, CHECK PORT 2
		LDA A	1,X				IF SO, LOAD DATA IN ACC A
		CMP A	#$1F			IS IT THE BREAK (^_) CHARACTER?
		BEQ		BRK				IF SO, GO BREAK PORT 2
T2		LDA B	#2				IF NOT, PREPARE TO TRANSMIT
		AND B	P2CR-P1CR,X		IS PORT 2 READY TO SEND?
		BEQ		T2				IF NOT, CHECK AGAIN
		STA A	P2DR-P1CR,X		IF SO, SEND DATA OUT PORT 2
		BRA		R1				AND GO CHECK PORT 1 FOR MORE DATA
R2		LDA A	#1				PREPARE TO CHECK PORT 2 FOR DATA
		AND A	P2CR-P1CR,X		ANY DATA FROM PORT 2?
		BEQ		R1				IF NOT, CHECK PORT 1
		LDA A	P2DR-P1CR,X		IF SO, LOAD DATA IN ACC A
T1		LDA B	#2				PREPARE TO TRANSMIT
		AND B	0,X				IS PORT 1 READY TO TRANSMIT?
		BEQ		T1				IF NOT, CHECK AGAIN
		STA A	1,X				IF SO, SEND DATA OUT PORT 1
		BRA		R2				AND GO CHECK PORT 2 FOR MORE DATA
BRK		LDA A	#$61			PREPARE TO BREAK PORT 2
		STA A	P2CR-P1CR,X		BREAK PORT 2
		JSR		DELAY			WAIT A BIT . . .
		BRA		INIT			AND START OVER

		END